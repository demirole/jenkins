import groovy.json.JsonSlurper

plugins {
    id 'groovy'
    id 'java'
    id "com.github.ben-manes.versions" version "0.28.0"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/releases/'
    }
    maven {
        url 'https://repo.spring.io/plugins-release/'
    }
}

sourceSets {
    initscripts {
        groovy {
            srcDirs = ['src']
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:3.0.4'
    compile 'org.jenkins-ci.main:jenkins-core:2.239'

    // Jenkins plugins
    compile group: 'org.jenkins-ci.plugins', name: 'credentials', version: '2.3.7', ext: 'jar'
    compile group: 'org.jenkins-ci.plugins', name: 'git', version: '4.2.2', ext: 'jar'
    compile group: 'org.jenkins-ci.plugins', name: 'gradle', version: '1.36', ext: 'jar'
    compile group: 'org.jenkins-ci.plugins', name: 'matrix-auth', version: '2.6.1', ext: 'jar'
    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-cps', version: '2.80', ext: 'jar'
    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-job', version: '2.39', ext: 'jar'

    // TRICKY: The lib folder contains all other plugins *JAR* files
    // if not found in Maven
    // compile fileTree(dir: 'lib', include: ['*.jar'])
    testCompile group: 'junit', name: 'junit', version: '4.13'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

task updatePluginsTxt {
    doLast {
        String url = "http://localhost:8080/pluginManager/api/json?depth=1"
        def json = new JsonSlurper().parseText(url.toURL().text)
        File pluginsTxt = new File("./plugins.txt")
        pluginsTxt.withWriter { writer ->
            json["plugins"].each { plugin ->
                writer.writeLine(plugin.shortName)
            }
        }
    }
}

